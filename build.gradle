apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidSdkBuildVersion

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'META-INF/LICENSE'
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 300907
        versionName "3.0.9.7"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            if (project.hasProperty('prodKeyStore') &&
                    project.hasProperty('prodKeyAlias') &&
                    project.hasProperty('prodKeyStorePassword') &&
                    project.hasProperty('prodKeyAliasPassword')) {
                storeFile file('../' + prodKeyStore)
                keyAlias prodKeyAlias
                storePassword prodKeyStorePassword
                keyPassword prodKeyAliasPassword
            } else {
				        storeFile commonDebugKeystore
				        storePassword 'android'
				        keyAlias 'androiddebugkey'
				        keyPassword 'android'
						}
        }
        btctestnet {
            if (project.hasProperty('testKeyStore') &&
                    project.hasProperty('testKeyAlias') &&
                    project.hasProperty('testKeyStorePassword') &&
                    project.hasProperty('testKeyAliasPassword')) {
                storeFile file('../' + testKeyStore)
                keyAlias testKeyAlias
                storePassword testKeyStorePassword
                keyPassword testKeyAliasPassword
            } else {
				        storeFile commonDebugKeystore
				        storePassword 'android'
				        keyAlias 'androiddebugkey'
				        keyPassword 'android'
						}
        }
        commonDebug {
            storeFile commonDebugKeystore
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        all {
            minifyEnabled false
        }
        debug {
            signingConfig signingConfigs.commonDebug
            applicationIdSuffix '.debug'
            debuggable true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    flavorDimensions "flavor"

    productFlavors {
        prodnet {
            applicationId 'com.mycelium.module.spvbch'
            versionName = defaultConfig.versionName
            signingConfig signingConfigs.release
            buildConfigField 'boolean', 'IS_TESTNET', 'false'
            buildConfigField 'String[]', 'TRUSTED_FULL_NODES', '{' +
                    '"tcp://bitcoin-abc-1.mycelium.com:8444",' +
                    '"tcp://bitcoin-abc-2.mycelium.com:8444",' +
                    '"tcp://bitcoin-abc-3.mycelium.com:8444",' +
                    '"tcp://bitcoin-abc-4.mycelium.com:8444"' +
                    '}'
            ext {
                appIdWallet = [release: "com.mycelium.wallet",
                               debug: "com.mycelium.wallet.debug"]
            }
        }
        btctestnet {
            applicationId 'com.mycelium.module.spvbch.testnet'
            versionName = defaultConfig.versionName + '-TESTNET'
            signingConfig signingConfigs.btctestnet
            buildConfigField 'boolean', 'IS_TESTNET', 'true'
            buildConfigField 'String[]', 'TRUSTED_FULL_NODES', '{' +
                    '"tcp://bitcoin-abc-1.mycelium.com:18444",' +
                    '"tcp://bitcoin-abc-2.mycelium.com:18444",' +
                    '"tcp://bitcoin-abc-3.mycelium.com:18444",' +
                    '"tcp://bitcoin-abc-4.mycelium.com:18444"' +
                    '}'
            ext {
                appIdWallet = [release: "com.mycelium.testnetwallet",
                               debug: "com.mycelium.testnetwallet.debug"]
            }
        }
    }
    applicationVariants.all { variant ->
        def flavor = variant.productFlavors[0]
        variant.buildConfigField "String", "appIdWallet", "\"${flavor.ext.appIdWallet[variant.buildType.name]}\""
    }
}

configurations {
    compile.exclude group: 'org.jetbrains' , module:'annotations'
}

dependencies {
    testImplementation "junit:junit:$junitVersion"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:support-v4:$androidSupportVersion"
    implementation "com.android.support:preference-v7:$androidSupportVersion"
    implementation 'com.android.support:multidex:1.0.3'

    implementation project(includePrefix +':modulespvcontract')
    implementation project(includePrefix +':wallet-android-modularization-tools:modularization-lib')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'com.google.protobuf:protobuf-java:3.4.0'
    implementation(guavaArtifact) {
        force = true
    }
    implementation project(includePrefix +':view')
    implementation project(includePrefix +':bitlib')
    implementation project(includePrefix +':bitcoincashj:core')
}

